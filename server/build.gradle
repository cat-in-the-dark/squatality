group 'com.catinthedark.squatality'
version '1.0-SNAPSHOT'

buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'io.vertx.core.Launcher'

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'com.catinthedark.squatality.server.MainVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.vertx:vertx-core:3.3.2"
    compile "com.corundumstudio.socketio:netty-socketio:1.7.10"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile project(':shared')
}

//Special for heroku
task stage(type: Copy, dependsOn: [clean, shadowJar]) {
    from "${shadowJar.archivePath}"
    into project.rootDir
    rename {
        'app.jar'
    }
}

stage.mustRunAfter(clean)
clean << {
    project.file('app.jar').delete()
}
