group 'com.catinthedark.squatality'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.4'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.catinthedark.squatality.server.ServerLauncher'

dependencies {
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile("com.esotericsoftware:kryonet:2.22.0-RC1") {
        exclude module: 'kryo'
    }
    compile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile project(':shared')
}

//Special for heroku
task stage(type: Copy, dependsOn: [clean, shadowJar]) {
    from "${shadowJar.archivePath}"
    into project.rootDir
    rename {
        'app.jar'
    }
}

stage.mustRunAfter(clean)
clean << {
    project.file('app.jar').delete()
}
